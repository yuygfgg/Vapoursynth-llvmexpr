# Copyright (C) 2025 yuygfgg
# 
# This file is part of Vapoursynth-llvmexpr.
# 
# Vapoursynth-llvmexpr is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
# 
# Vapoursynth-llvmexpr is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with Vapoursynth-llvmexpr.  If not, see <https://www.gnu.org/licenses/>.

@ifndef __SINGLEEXPR__
@error This expr should be used in SingleExpr mode
@endif

# Configuration and Initialization
min_size_thr = 100;
bg_val = 0;
fg_val = __OUTPUT_BITDEPTH__ == 32 ? 1 : 2 ** (__OUTPUT_BITDEPTH__ - 1);

w = $width;
h = $height;
max_labels = (w * h) / 2 + 2;

label_map = new(w * h);
parent = new(max_labels);
area = new(max_labels);

dx = new(4); # Delta X
dy = new(4); # Delta Y
dx[0] = -1; dy[0] =  0;
dx[1] = -1; dy[1] = -1;
dx[2] =  0; dy[2] = -1;
dx[3] =  1; dy[3] = -1;

# Initialize
i = 0;
while (i < max_labels) {
    parent[i] = i;
    area[i] = 0;
    i = i + 1;
}
i = 0;
while (i < w * h) {
    label_map[i] = 0;
    i = i + 1;
}

<global<parent>>
function find_root(Value i) {
    root = i;
    while (parent[root] != root) {
        root = parent[root];
    }
    while (parent[i] != root) {
        next_i = parent[i];
        parent[i] = root;
        i = next_i;
    }
    return root;
}

<global<parent>>
function union_sets(Value i, Value j) {
    i_root = find_root(i);
    j_root = find_root(j);
    if (i_root != j_root) {
        if (i_root < j_root) {
            parent[j_root] = i_root;
        } else {
            parent[i_root] = j_root;
        }
    }
}

# Labeling and Recording Equivalences
next_label = 1;
y = 0;
while (y < h) {
    x = 0;
    while (x < w) {
        pixel_val = dyn($x, x, y, 0);

        if (pixel_val != bg_val) {
            neighbor_labels = new(4);
            n_count = 0;
            
            k = 0;
            while (k < 4) {
                nx = x + dx[k];
                ny = y + dy[k];

                # Boundary check (ny is always <= y, so only need ny >= 0)
                if (nx >= 0 && nx < w && ny >= 0) {
                    if (dyn($x, nx, ny, 0) != bg_val) {
                        neighbor_labels[n_count] = label_map[ny * w + nx];
                        n_count = n_count + 1;
                    }
                }
                k = k + 1;
            }

            if (n_count == 0) {
                label_map[y * w + x] = next_label;
                next_label = next_label + 1;
            } else {
                current_label = neighbor_labels[0];
                k = 1;
                while(k < n_count) {
                    current_label = min(current_label, neighbor_labels[k]);
                    k = k + 1;
                }
                
                label_map[y * w + x] = current_label;
                
                k = 0;
                while(k < n_count) {
                    union_sets(current_label, neighbor_labels[k]);
                    k = k + 1;
                }
            }
        }
        x = x + 1;
    }
    y = y + 1;
}

# Resolve Labels and Calculate Area
y = 0;
while (y < h) {
    x = 0;
    while (x < w) {
        idx = y * w + x;
        if (label_map[idx] > 0) {
            root = find_root(label_map[idx]);
            label_map[idx] = root;
            area[root] = area[root] + 1;
        }
        x = x + 1;
    }
    y = y + 1;
}

# Generate Final Output
y = 0;
while (y < h) {
    x = 0;
    while (x < w) {
        final_val = bg_val;
        label = label_map[y * w + x];
        if (label > 0) {
            if (area[label] >= min_size_thr) {
                final_val = fg_val;
            }
        }
        store(x, y, 0, final_val);
        x = x + 1;
    }
    y = y + 1;
}