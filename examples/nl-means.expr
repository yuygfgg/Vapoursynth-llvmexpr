# Copyright (C) 2025 yuygfgg
# 
# This file is part of Vapoursynth-llvmexpr.
# 
# Vapoursynth-llvmexpr is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
# 
# Vapoursynth-llvmexpr is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with Vapoursynth-llvmexpr.  If not, see <https://www.gnu.org/licenses/>.

@ifndef __EXPR__
@error This expr should be used in Expr mode
@endif

@define PATCH_RADIUS 2
@define SEARCH_RADIUS 5
@define FILTER_STRENGTH 10.0
@define H_SQUARED (FILTER_STRENGTH * FILTER_STRENGTH)

function patch_distance(cx1, cy1, cx2, cy2) {
    sum_sq_diff = 0.0
    py = -PATCH_RADIUS
    while (py <= PATCH_RADIUS) {
        px = -PATCH_RADIUS
        while (px <= PATCH_RADIUS) {
            pixel1 = dyn($x, cx1 + px, cy1 + py)
            pixel2 = dyn($x, cx2 + px, cy2 + py)
            diff = pixel1 - pixel2
            sum_sq_diff = sum_sq_diff + (diff * diff)
            px = px + 1
        }
        py = py + 1
    }
    return sum_sq_diff
}

weighted_sum = 0.0
total_weight = 0.0
sy = $Y - SEARCH_RADIUS
while (sy <= $Y + SEARCH_RADIUS) {
    sx = $X - SEARCH_RADIUS
    while (sx <= $X + SEARCH_RADIUS) {
        distance = patch_distance($X, $Y, sx, sy)
        weight = exp(-distance / H_SQUARED)
        search_pixel_value = dyn($x, sx, sy)
        total_weight = total_weight + weight
        weighted_sum = weighted_sum + (search_pixel_value * weight)
        sx = sx + 1
    }
    sy = sy + 1
}
RESULT = total_weight > 0 ? weighted_sum / total_weight : $x